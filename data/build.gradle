apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: "${project.rootDir}/buildsystem/jacoco.gradle"

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildTools"]

    defaultConfig {
        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]
        versionCode globalConfiguration["androidVersionCode"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField 'String', "ApiKey", THE_MOVIE_DB_API_KEY
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', "ApiKey", THE_MOVIE_DB_API_KEY
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }
}

dependencies {

    def dataDependencies = rootProject.ext.dataDependencies
    def dataKapt = rootProject.ext.dataKapt
    def testDependencies = rootProject.ext.dataTestDependencies
    def allDependencies = rootProject.ext.allDependencies

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':domain')

    dataDependencies.each { v -> implementation v }
    dataKapt.each { v -> kapt v }

    //network
    implementation (allDependencies.retrofit2GsonConverter) {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    implementation 'ru.gildor.coroutines:kotlin-coroutines-retrofit:0.11.0'

    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'


    //unit tests
    testDependencies.each { v -> testImplementation v }

    androidTestImplementation('com.android.support.test:runner:1.0.2') {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}
